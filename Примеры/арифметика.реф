*
* Реализует сложение и умножение целых неотрицательных чисел с произвольным числом разрядов
* для произвольных основания системы счисления и набора символов-цифр.
*
* Автор: Даниил Исакевич.
*
* https://forum.altlinux.org/index.php?topic=44627.msg373938#msg373938
*

начало =
    <напечатать "Таблица умножения">
    <таблицаумножения>
    <напечатать "\n" "Таблица сложения">
    <таблицасложения>
    <напечатать>
    <напечатать "I8!=" <убрать-скобки <факториал ("I8")>>>
    <напечатать "IZ2!=" <убрать-скобки <факториал ("IZ2")>>>
    <напечатать>
    <сложитьчисла
        <перемножитьчисла ("I23456789") ("98765432I")>
        ("I2345432I")>;
* Поле зрения по завершении: (I2I93263I236Z8959Z)

*Задаётся нужный набор цифр-символов,
*основание системы счисление - длина этого набора:
*всецифры = "0" "1" "2" "3" "4" "5" "6" "7" "8" "9" "A" "B" "C" "D" "E" "F";
всецифры = "Z" "I" "2" "3" "4" "5" "6" "7" "8" "9";
* для разнообразия нуль обозначен Z (zero), единица I

* Положительное целое число записываем как набор разрядов
* (младшие справа, старшие слева), заключённый в круглые скобки.


* Вычисление факториала
факториал {
    (.разряды) = <факториал (.разряды) <всецифры>>;
    (?цифрануль) ?цифрануль ?цифраодин .остальные = (?цифраодин);
    (.разряды) .всецифры =
        <перемножитьчисла
            (.разряды)
            <факториал <предыдущеечисло (.разряды)>>>;
}


* Вывод таблицы сложения и таблицы умножения

таблицаумножения = <вывести-таблицу "*">;
таблицасложения = <вывести-таблицу "+">;

вывести-таблицу {
    ?знак =
        <напечатать <с-табуляцией ?знак <всецифры>>>
        <вывести-таблицу ?знак (<всецифры>) (<всецифры>)>;
    ?знак () (.всецифры) = ;
    ?знак (?цифра .остальные) (.всецифры) =
        <напечатать
            ?цифра
            <строка-таблицы ?знак ?цифра (.всецифры)>>
        <вывести-таблицу ?знак (.остальные) (.всецифры)>;
}

строка-таблицы {
    ?знак ?цифра1 () = ;
    ?знак ?цифра1 (?цифра2 .остальные) =
        "\t"
        <убрать-скобки <вычислить ?знак (?цифра1) (?цифра2)>>
        <строка-таблицы ?знак ?цифра1 (.остальные)>; 
}

вычислить {
    "+" (.число1) (.число2) = <сложитьчисла (.число1) (.число2)>;
    "*" (.число1) (.число2) = <перемножитьчисла (.число1) (.число2)>;
}


* Функции декремента, инкремента, сложения, умножения

перемножитьчисла {
    (.разряды1) () = ();
    (.разряды1) (.старшиеразряды2 ?младшийразряд2) =
        <сложитьчисла
            <приписать-цифру
                <перемножитьчисла (.разряды1) (.старшиеразряды2)>
                <нуль>>
            <умножитьчислонацифру (.разряды1) ?младшийразряд2>>;
}

умножитьчислонацифру {
    (.разряды) ?цифра = <умножитьчислонацифру (.разряды) ?цифра <всецифры>>;
    (.разряды) ?цифрануль ?цифрануль .остальные = (?цифрануль);
    (.разряды) ?цифраодин ?цифрануль ?цифраодин .остальные = (.разряды);
    (.разряды) ?цифра .всецифры =
        <сложитьчисла
            (.разряды)
            <умножитьчислонацифру
                (.разряды)
                <предыдущаяцифра ?цифра>>>;
}

сложитьчисла {
    (.разряды1) (.разряды2) = <сложитьчисла (.разряды1) (.разряды2) <всецифры>>;
    (.старшиеразряды1 ?младшийразряд1) (.старшиеразряды2 ?цифрануль)
    ?цифрануль .остальные =
        <приписать-цифру
            <сложитьчисла
                (.старшиеразряды1)
                (.старшиеразряды2)>
            ?младшийразряд1>;
    (.старшиеразряды1 ?цифрануль) (.старшиеразряды2 ?младшийразряд2)
    ?цифрануль .остальные =
        <сложитьчисла
            (.старшиеразряды2 ?младшийразряд2)
            (.старшиеразряды1 ?цифрануль)>;
    () (.разряды) .всецифры = (.разряды);
    (.разряды) () .всецифры = (.разряды);
    (.старшиеразряды1 ?младшийразряд1) (.старшиеразряды2 ?младшийразряд2)
    .всецифры =
        <сложитьчисла
            <следующеечисло (.старшиеразряды1 ?младшийразряд1)>
            (.старшиеразряды2 <предыдущаяцифра ?младшийразряд2>)>;
}

следующеечисло {
    () = (<один>);
    (.разряды) = <следующеечисло (.разряды) (<всецифры>)>;
    (.старшиеразряды ?младшийразряд) (.до ?младшийразряд ?следующ .после) =
        (.старшиеразряды ?следующ);
    (.старшиеразряды ?младшийразряд) (?перваяцифра .между ?младшийразряд) =
        <приписать-цифру
            <следующеечисло (.старшиеразряды)>
            ?перваяцифра>;
}

предыдущеечисло {
    (.разряды) = <предыдущеечисло (.разряды) <всецифры>>;
    (.старшиеразряды ?цифрануль) ?цифрануль .между ?последняяцифра =
        <приписать-цифру
            <убрать-ведущие-нули <предыдущеечисло (.старшиеразряды)>>
            ?последняяцифра>;
    (.старшиеразряды ?цифра) .до ?предыдущ ?цифра .после =
        (.старшиеразряды ?предыдущ);
}

убрать-ведущие-нули {
    (.разряды) = <убрать-ведущие-нули (.разряды) <всецифры>>;
    (?цифрануль .остальныеразряды) ?цифрануль .остальныецифры =
        <убрать-ведущие-нули (.остальныеразряды)>;
    (.разряды) .всецифры = (.разряды);
}

предыдущаяцифра {
    ?цифра = <предыдущаяцифра ?цифра <всецифры>>;
    ?цифра .до ?предыдущ ?цифра .после = ?предыдущ;
    ?цифра ?цифра .между ?последняя = ?последняя;
}

приписать-цифру {
    (.разряды) ?цифра = (.разряды ?цифра);
}

нуль {
    = <нуль <всецифры>>;
    ?цифрануль .остальные = ?цифрануль;
}

один {
    = <один <всецифры>>;
    ?цифрануль ?цифраодин .остальные = ?цифраодин;
}


* Функции для оформления

убрать-скобки {
    (.разряды) = .разряды;
}
с-табуляцией {
    = ;
    ?символ .прочие = ?символ "\t" <с-табуляцией .прочие>;
}
напечататьчисло {(.разряды) = <напечатать .разряды>;}
напечатать {.чтонибудь = <Prout .чтонибудь>;}
напечатать-промежуточный {.что-нибудь = <напечатать .что-нибудь> .что-нибудь;}
