*
* Преобразует числа от 0 до 99 в числительные, и основание от 2 до 20 - в название системы счисления.
*
* Автор: Даниил Исакевич.
*
* https://forum.altlinux.org/index.php?topic=44627.msg373938#msg373938
*

начало =
    <вывестичисласловами ("99")>
    <вывестисистемысчисления ("20")>;

вывестичисласловами {
    ("0") = ;
    (.число) =
        <вывестичисласловами <предыдущеечисло (.число)>>
        <напечатать .число " = " <числословами .число>>;
}
вывестисистемысчисления {
    ("1") = ;
    (.число) =
        <вывестисистемысчисления <предыдущеечисло (.число)>>
        <напечатать "по основанию " .число " - " <системасчисленияот2до20 .число>>;
}

системасчисленияот2до20 {
    .число = <модиф3 <числословами .число>> "ичная";
}
модиф3 {
    "два" = "дво";
    "три" = "тро";
    "четыре" = "четвер";
    "восемь" = "восьмир";
    "десять" = "десят";
    .название "ь" = .название "ир";
}

числословами {
    "0" = ;
    "1" = "один";
    "2" = "два";
    "3" = "три";
    "4" = "четыре";
    "5" = "пять";
    "6" = "шесть";
    "7" = "семь";
    "8" = "восемь";
    "9" = "девять";
    "10" = "десять";
    "1" ?цифра = <модиф1 <числословами ?цифра>> "надцать";
    "4" ?цифра = "сорок " <числословами ?цифра>;
    "9" ?цифра = "девяносто " <числословами ?цифра>;
    ?цифра1 "0" = <модиф2 <числословами ?цифра1>>;
    ?цифра1 ?цифра2 = <модиф2 <числословами ?цифра1>> " " <числословами ?цифра2>;
}
модиф1 {
    .название "а" = .название "е";
    .название "е" = .название;
    .название "ь" = .название;
    .название = .название;
}
модиф2 {
    .название "ь" = .название "ьдесят";
    .название = .название "дцать";
}

следующеечисло {
    () = (<один>);
    (.разряды) = <следующеечисло (.разряды) (<всецифры>)>;
    (.старшиеразряды ?младшийразряд) (.до ?младшийразряд ?следующ .после) =
        (.старшиеразряды ?следующ);
    (.старшиеразряды ?младшийразряд) (?перваяцифра .между ?младшийразряд) =
        <приписать-цифру
            <следующеечисло (.старшиеразряды)>
            ?перваяцифра>;
}
предыдущеечисло {
    (.разряды) = <предыдущеечисло (.разряды) <всецифры>>;
    (.старшиеразряды ?цифрануль) ?цифрануль .между ?последняяцифра =
        <приписать-цифру
            <убрать-ведущие-нули <предыдущеечисло (.старшиеразряды)>>
            ?последняяцифра>;
    (.старшиеразряды ?цифра) .до ?предыдущ ?цифра .после =
        (.старшиеразряды ?предыдущ);
}

убрать-ведущие-нули {
    (.разряды) = <убрать-ведущие-нули (.разряды) <всецифры>>;
    (?цифрануль .остальныеразряды) ?цифрануль .остальныецифры =
        <убрать-ведущие-нули (.остальныеразряды)>;
    (.разряды) .всецифры = (.разряды);
}

предыдущаяцифра {
    ?цифра = <предыдущаяцифра ?цифра <всецифры>>;
    ?цифра .до ?предыдущ ?цифра .после = ?предыдущ;
    ?цифра ?цифра .между ?последняя = ?последняя;
}

приписать-цифру {
    (.разряды) ?цифра = (.разряды ?цифра);
}

нуль {
    = <нуль <всецифры>>;
    ?цифрануль .остальные = ?цифрануль;
}

один {
    = <один <всецифры>>;
    ?цифрануль ?цифраодин .остальные = ?цифраодин;
}


всецифры = "0" "1" "2" "3" "4" "5" "6" "7" "8" "9";
убрать-скобки {
    (.разряды) = .разряды;
}
напечататьчисло {(.разряды) = <напечатать .разряды>;}
напечатать {.чтонибудь = <Prout .чтонибудь>;}
напечатать-промежуточный {.что-нибудь = <напечатать .что-нибудь> .что-нибудь;}
