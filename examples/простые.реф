#!/bin/refal -w

// Вычисления не являются типовым применением РЕФАЛ.
// Оценивается производительность управления памятью.

начало = <натуральные <решето <ряд от 2 до 10000>>>;

решето {
  0    … = 0    <решето …>;
  ?шаг … = ?шаг <решето <кратные ?шаг ?шаг (…)>>;
   = ;
}

кратные {
  ?шаг 1     (? .вход) .выход = <кратные ?шаг ?шаг        (.вход) .выход 0>;
  ?шаг ?счёт (? .вход) .выход = <кратные ?шаг <?счёт - 1> (.вход) .выход ?>;
  … () .готово = .готово;
}

ряд {
  от ?    до ?     .ряд = .ряд ?;
  от ?мин до ?макс .ряд = <ряд от <1 + ?мин> до ?макс .ряд ?мин>;
}

натуральные {
  0 … =   <натуральные …>;
  ? … = ? <натуральные …>;
  = ;
}

